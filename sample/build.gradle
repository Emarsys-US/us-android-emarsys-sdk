apply plugin: 'com.android.application'

group = 'com.emarsys.sample'

android {
    buildToolsVersion Config.buildToolsVersion
    compileSdkVersion Config.targetApiLevel
    defaultConfig {
        applicationId "com.emarsys.sample"
        minSdkVersion Config.minApiLevel
        targetSdkVersion Config.targetApiLevel
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "com.emarsys.sample.testutils.SampleAppTestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    if (project.devConfig.releaseSampleApp || System.env.DEVELOPMENT_MODE) {
        signingConfigs {
            release {
                storeFile file(System.env.RELEASE_STORE_FILE ?: releaseStoreFile)
                storePassword System.env.RELEASE_STORE_PASSWORD ?: releaseStorePassword
                keyAlias System.env.RELEASE_KEY_ALIAS ?: releaseKeyAlias
                keyPassword System.env.RELEASE_KEY_PASSWORD ?: releaseKeyPassword
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }
    buildTypes {
        debug {
            multiDexKeepProguard file("proguard-multidex-rules.pro")
            multiDexEnabled true
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.1.0'
    }
}

repositories {
    maven {
        url "http://dl.bintray.com/emarsys-mobile/android"
    }
}

dependencies {
    if (project.devConfig.useLocalDependency || System.env.DEVELOPMENT_MODE) {
        implementation project(':emarsys')
    } else {
        implementation 'com.emarsys:emarsys-sdk:+'
    }
    // Getting a "Could not find firebase-core" error? Make sure you have
    // the latest Google Repository in the Android SDK manager

    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.google.firebase:firebase-messaging:17.3.4', {
        exclude group: 'com.android.support'
        exclude group: 'androidx'
    }
    implementation 'com.google.firebase:firebase-core:16.0.5', {
        exclude group: 'com.android.support'
        exclude group: 'androidx'
    }
    implementation 'com.squareup.picasso:picasso:2.71828', {
        exclude group: 'com.android.support'
        exclude group: 'androidx'
    }

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation "androidx.test:runner:1.1.0"
    androidTestImplementation "androidx.test:rules:1.1.0"
    androidTestImplementation "androidx.test.ext:junit:1.0.0"

}

apply plugin: 'com.google.gms.google-services'