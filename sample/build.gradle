apply plugin: 'com.android.application'

android {
    compileSdkVersion Config.targetApiLevel
    defaultConfig {
        applicationId "com.emarsys.sample"
        minSdkVersion Config.minApiLevel
        targetSdkVersion Config.targetApiLevel
        testInstrumentationRunner "com.emarsys.sample.testutils.SampleAppTestRunner"
    }

    if (project.devConfig.releaseSampleApp || System.env.DEVELOPMENT_MODE) {
        signingConfigs {
            release {
                storeFile file(System.env.RELEASE_STORE_FILE ?: releaseStoreFile)
                storePassword System.env.RELEASE_STORE_PASSWORD ?: releaseStorePassword
                keyAlias System.env.RELEASE_KEY_ALIAS ?: releaseKeyAlias
                keyPassword System.env.RELEASE_KEY_PASSWORD ?: releaseKeyPassword
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.gms:google-services:3.1.2'
    }
}

repositories {
    maven {
        url "http://dl.bintray.com/emarsys-mobile/android"
    }
}

dependencies {
    if (project.devConfig.useLocalDependency || System.env.DEVELOPMENT_MODE) {
        implementation project(':emarsys')
    } else {
        implementation 'com.emarsys:emarsys-sdk:+'
    }
    // Getting a "Could not find firebase-core" error? Make sure you have
    // the latest Google Repository in the Android SDK manager

    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation 'com.squareup.picasso:picasso:2.5.2'

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation project(':testUtils')
}

apply plugin: 'com.google.gms.google-services'